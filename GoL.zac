// The one and only Conway's Game of Life
//
// Play below by running: zac GoL.zac

// #grid
// |     *****               *    |
// |  * *** **       *   *        |
// |     ** **    *    ***        |
// |   * ** *                     |
// |     ** *    *     *     *    |
// |     ** *        *            |
// |     ** *    *                |
// |     ** *             *       |
// |     ** **        *  *        |
// |     ** **                    |
// |     ** **              *     |
// |     *****        *           |

let pipe = // |
let star = // *
let space = chr(32)

let width = 1
while (not(eq(#grid(width), pipe))) {
  let width = add(width, 1)
}
let width = add(width, -1)

defn get_coord(x, y) {
  // 1 for the first left bar
  let offset = 1
  // 1 offset for each left, right pipe and each newline if y > 0
  if (gt(y, 0)) {
    let offset = add(offset, mul(y, 3))
  }
  let offset = add(offset, mul(y, width))
  let offset = add(offset, x)
  let char = #grid(offset)
  let ret = 0
  if (eq(char, star)) {
    let ret = 1
  }
  ret
}

defn neighbors(x, y) {
  let num_alive_neighbs = 0
  let dx = -1
  while (lt(dx, 2)) {
    let dy = -1
    while (lt(dy, 2)) {
      if (not(and(eq(dx, 0), eq(dy, 0)))) {
        let new_x = add(dx, x)
        let new_y = add(dy, y)
        let num_alive_neighbs = add(num_alive_neighbs, get_coord(new_x, new_y))
      }
      let dy = add(dy, 1)
    }
    let dx = add(dx, 1)
  }
  num_alive_neighbs
}

